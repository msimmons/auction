buildscript {

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'org.springframework:springloaded:1.2.1.RELEASE'
    }
}

ext {
    snapshotVersion = "$releaseVersion-SNAPSHOT"
}
ext["currentVersion"] = hasProperty("release") ? releaseVersion : snapshotVersion;

allprojects {
    apply plugin: 'maven'

    group = "net.contrapt"
    version = currentVersion;
}
description = 'Auction Application'


task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'groovy'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

        testCompile("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
        testCompile("org.codehaus.groovy:groovy:$groovyVersion")
        testCompile("org.spockframework:spock-core:$spockVersion")
        testCompile("org.spockframework:spock-spring:$spockVersion")
    }

// Define system properties required by tests
    test {
        testLogging.showStandardStreams = true
        testLogging.exceptionFormat = "full"
        systemProperties = [
            'test.dataDirectory': "$buildDir/resources/test"
        ]
    }
}

